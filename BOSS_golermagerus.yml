# -----------------------------------
# Golermagerus Boss Encounter
# @author: Tyler J. Sawyer
# @version: 1.0
# Last Updated: 18:01 on May 16, 2014 (Friday)
# -----------------------------------
#
# Phase 1 : 100% - 50%
# If any player is within 3 blocks of the boss, that player
# is instantly killed. This is a pure ranged damage fight
# (spells, bows, etc.)
#
# Color Blast - 10 second cooldown
# - Afflict players with random color flags
# - It will narrate in their chat what color they are.
# - [GREEN] - Players must run to the Green Glass.
# - [RED] - Players must run to the Red Glass.
# - - These flags last for 10 seconds.
# - - - The flags are removed early if the player enters
# - - -   the red or green glass area, depending on their flag.
# - - - - If they enter the correct area, they will damgae
# - - - -   the boss for 100 damage.
# - - - - If they enter the wrong area, they will instantly die and
# - - - -   heal the boss for 1000 HP.
# - - - - After 10 seconds, if the Players flags are still active,
# - - - -   the player will instantly die, and the boss will heal 
# - - - -   1000 HP for each player killed.
# 
# psuedo code of the mechanic:
# on enter notable
# if player.flag[golermag_green] == true { 
# - teleport player center room 
# - flag player [golermag_green]:!
# - hurt 1000 <npc> }
#
# THE BOMB:
# Every 10 seconds, flag a player as the bomb.
# They will a notification telling them of their bomb thing.
# They have 10 seconds before the bomb 'detonates'.
# For all players within 40 blocks of the bomb, they will instantly die.
# If the player with the bomb reaches the PURPLE glass before the 10 second
# flag is up, the boss will take 200 damage, and the player's bomb flag
# will be removed (and they will take no damage).
#
# Phase 2: 49% - 0%
# Burn Phase
# Unlike other bosses, this boss stops doing Color Blasts (but continues
# to perform bombs on random players)
# 
# "Burn Phase Effect"
# On boss damaged, multiply the damage amount by 4. (4x damage)
#
# "Burn Phase Harming Effect"
# Every 15 seconds, increment a boss flag by 1.
# Then, take this flag and multiply it by a static damage
# amount (starting at 2 health).
# - Every 15 seconds, this damage increases by 2.
# - After 1 minute, the boss hurts everyone for 8 damage.
# - After 2 minutes, the boss hurts everyone for 16 damage.
# etc etc.
#

golermagerus:
  type: assignment

  default constants:
    agro message: Puny Humans! You die now!
    death message: My arms..legs..tired..so..sleepy...
    victory message: Break your bones! Hahaha!
    respawn message: HNYGH..Must..escape..kill..KILL!
    
    # Special Event Messages
    colorburst_msg: Now YOU feel pain!
    bomb_msg: Puny human go BOOM!
    burnPhaseMsg: NYRRAGHH! Enough of this petty nonsense!
    burnPhaseHarm: So much ENERGY!
    
    # Special Warp Points
    Golermagerus_warpPoint: "-94.97847,57,-38.01033,world_the_end"

    # Boss Stats
    # Health Pool
    Golermagerus_health: 25000
    # Damage / Swing
    Golermagerus_str: 2
    # Armor Value
    Golermagerus_AC: 1
    # Agro Radius (in Blocks)
    Golermagerus_AGRO_RADIUS: 30
    # Movement Speed (Blocks / Tick)
    Golermagerus_Movement_Speed: 0
    # Spawnpoint
    Golermagerus_SpawnPoint: "-105.55,57,-38,world_the_end"
    # Respawn Timer in seconds (3600 = 1hr)
    Golermagerus_Respawn_Timer: 86400
    # XP Amount he drops on death
    Golermagerus_XP_AMT: 7500
    
  interact scripts:
  - 10 GolermagerusScript
    
  actions:
    on assignment:
    - trigger name:proximity toggle:true "radius:<cons:Golermagerus_AGRO_RADIUS>"
    - trigger name:damage toggle:true

    # Setup stats
    - execute as_server "npc sel <npc.id>"
    - execute as_server "npc type GIANT"
    - execute as_server "npc speed <cons:Golermagerus_Movement_Speed>"
    - execute as_server "npc lookclose"
    - execute as_server "trait sentry"
    - execute as_server "sentry health <cons:Golermagerus_health>"
    - execute as_server "sentry respawn <cons:Golermagerus_Respawn_Timer>"
    - execute as_server "sentry speed <cons:Golermagerus_Movement_Speed>"
    - execute as_server "sentry strength <cons:Golermagerus_str>"
    - execute as_server "sentry armor <cons:Golermagerus_AC>"
    - execute as_server "sentry range <cons:Golermagerus_AGRO_RADIUS>"

    on spawn:
    - announce "<red><npc.name> yells<&cm> <&sq><cons:respawn message><&sq>"
    # Warp to its spawn point on spawn.
    - teleport <npc> "location:<cons:Golermagerus_SpawnPoint>"
    # Clear previous targets from hate list
    - execute as_server "npc sel <npc.id>"
    - execute as_server "sentry target clear"
    # Clear boss flags
    - flag npc BossPlayerList:!
    - flag npc Golermagerus_Colorburst_CD:!
    - flag npc Golermagerus_Bomb_CD:!
    - flag npc Golermagerus_Harm_CD:!
    - flag npc Golermagerus_BurnPhase_Active:!
    - flag npc Golermagerus_HarmPhase_Amt:!
    - flag npc Golermagerus_Grace_CD:!
    - flag npc Golermagerus_BombGrace_CD:!

    on death:
    # Drop the right bindings 100% of the time.
    - drop "TF_BindingRight" qty:1 location:<npc.location>
    # Drop an enderdragon ore 100% of the time.
    - drop "EnderdragonOre" qty:1 location:<npc.location>
    # Random Loot Item 1
    - random 6
    - drop "Artemis" qty:1 location:<npc.location>
    - drop "Mithaniel" qty:1 location:<npc.location>
    - drop "Golem_Helm" qty:1 location:<npc.location>
    - drop "Golem_Chest" qty:1 location:<npc.location>
    - drop "Golem_Legs" qty:1 location:<npc.location>
    - drop "Golem_Boots" qty:1 location:<npc.location>
    # Random Loot Item 2
    - random 6
    - drop "Artemis" qty:1 location:<npc.location>
    - drop "Mithaniel" qty:1 location:<npc.location>
    - drop "Golem_Helm" qty:1 location:<npc.location>
    - drop "Golem_Chest" qty:1 location:<npc.location>
    - drop "Golem_Legs" qty:1 location:<npc.location>
    - drop "Golem_Boots" qty:1 location:<npc.location>
    # Reset npc effects and clear hate targets.
    - execute as_server "npc sel <npc.id>"
    - execute as_server "npc effect --play none"
    - execute as_server "sentry target clear"
    # Announce to players the boss is dead.
    - announce "<red><npc.name> yells, <&sq><cons:death message><&sq>"
    - announce "<&5><npc.name> has been slain!"
    - foreach <npc.flag[BossPlayerList].aslist> {
      - execute as_server "xp <cons:Golermagerus_XP_AMT> <%value%.name>"
      - adjust %value% "show_boss_bar:"
      - flag player:%value% LBOARD_B:++
      - flag player:%value% LBOARD_SCORE_TOTAL:<math.as_int:<<player>.flag[LBOARD_SCORE_TOTAL]>+100>
      - flag player:%value% Golermagerus_Engaged:!
      - flag player:%value% "ENDER_golermagerusKills:++"
      - narrate targets:<%value%.name> "<aqua>You assisted in slaying <npc.name>."
      }

    on enter proximity:
    - announce "<red><npc.name> yells<&cm> <&sq><cons:agro message><&sq>"
    - playsound location:<npc.location> sound:zombie_idle pitch:0.7
    # Flag the player as 'engaged' with the boss for 30 seconds.
    # If the player attacks the NPC, this refreshes.
    # If the player gets summoned, this refreshes.
    # If the player warps to spawn or home, this duration will fade out.
    - flag player "Golermagerus_Engaged:true" duration:30
    # Adjust BossBar Health
    - adjust <player> "show_boss_bar:<m.as_int:<npc.health.percentage.as_int>*2>|<npc.name> - <npc.health.percentage.as_int>%"

    on exit proximity:
    # summon player if they are still engaged by the boss.
    - if "<flag.p:Golermagerus_Engaged:true || false>" == true {
      - announce "<red><npc.name> yells<&cm> <&sq>You will not evade me<&cm> <player.name>!<&sq>"
      - narrate "<yellow>You have been summoned!"
      - teleport <player> "location:<cons:Golermagerus_warpPoint>"
      - playsound location:<player.location> sound:portal_travel
      - playeffect location:<player.location> effect:portal qty:1000
      }
    - adjust <player> "show_boss_bar:"
    
    on kill:
    - announce "<red><npc.name> yells<&cm> <&sq><cons:victory message><&sq>"

"GolermagerusScript":
   type: interact
   steps:
     1:
       click trigger:
         script:
         # Right Clicking on the boss tells you his % health.
         - narrate "<yellow><npc.name><&co> <npc.health.percentage.as_int>%"
       proximity trigger:
         entry:
           script:
           # Add player to the boss list when they enter his proximity.
           - if <npc.flag[BossPlayerList].aslist> !contains <player> && <player.name> != <npc.name> flag npc BossPlayerList:->:<player>
       damage trigger:
         script:
           # Teleport the boss to its initial position (prevent knocking the boss around)
           - teleport <npc> "location:<cons:Golermagerus_SpawnPoint>"
           # Ensure player is flagged as fighting the boss.
           - if <npc.flag[BossPlayerList].aslist> !contains <player> && <player.name> != <npc.name> flag npc BossPlayerList:->:<player>
           # When the boss is hit, re-flag the player as engaged with the boss.
           - flag player "Golermagerus_Engaged:true" duration:30
           - playsound location:<npc.location> sound:zombie_hurt pitch:0.7
           - playeffect location:<npc.location> effect:RED_DUST qty:10
           # Adjust BossBar Health
           - if <npc.health.percentage.as_int> == null {
             - adjust <player> "show_boss_bar:"
             }
             else {
             - adjust <player> "show_boss_bar:<m.as_int:<npc.health.percentage.as_int>*2>|<npc.name> - <npc.health.percentage.as_int>%"
             }
           # Kill anyone within meele range.
           - run "Golermagerus_kill_meele"
           - run "Golermagerus_initiate_Phase2"
           # Here are the boss special events that fire off throughout the fight.
           # Note - These two only fire off between 98% and 50% health.
           # Then, the boss swaps to phase 2 abilities.
           - if <npc.flag[Golermagerus_Grace_CD]> != null queue clear
           - if <npc.flag[Golermagerus_Grace_CD]> == null flag npc "Golermagerus_Grace_CD:true" duration:1
           - ^if <npc.health.percentage.as_int> <= 99 && <npc.health.percentage.as_int> >= 50 {
             - random 2
             - run "Golermagerus_ColorBurst_Ability"
             - run "Golermagerus_DaBomb_Ability"
             }
           - ^if <npc.health.percentage.as_int> <= 49 {
             - run "Golermagerus_Phase2_HarmCompounder"
             - define bonusDmg <math.as_int:<context.damage>*3>
             - narrate "<&9>Your attack did an additional %bonusDmg% damage to Golermagerus."
             - hurt %bonusDmg% <n@Golermagerus>
             }
   
# -----------------------------------
# Abilities
# -----------------------------------   
"Golermagerus_kill_meele":
   type: task
   # This ability kills anyone in meele range of the boss.
   script:
     - teleport <npc> "location:<cons:Golermagerus_SpawnPoint>"
     - foreach <<npc.location>.find.players.within[6].aslist> {
       - narrate targets:%value% "<red>Golermagerus stomps you to death!"
       - playsound location:<npc.location> sound:hurt_flesh
       - playeffect location:<%value%.location> effect:RED_DUST qty:1000
       - hurt 10000 %value%
       }

"Golermagerus_ColorBurst_Ability":
   type: task
   # This ability changes a player flag every 10 seconds. See the
   # initial script comments at the top of this file for details.
   script:
     - if <npc.flag[Golermagerus_Colorburst_CD]> == null {
       - flag npc "Golermagerus_Colorburst_CD:true" duration:10
       - playsound location:<npc.location> sound:zombie_idle pitch:0.7
       - announce "<red><npc.name> yells<&cm> <&sq><cons:colorburst_msg><&sq>"
       - foreach <<npc.location>.find.players.within[40].aslist> {
         - if <%value%.flag[Golermagerus_Ability_GCD]> == "ON" queue clear
         - if <%value%.flag[Golermagerus_Color]> == "red" {
           - narrate targets:%value% "<aqua>You have failed to remove your color assignment!"
           - announce "<red><%value%.name> failed to remove their color assignment and healed the boss for 1000 HP!"
           - playsound location:<%value%.location> sound:hurt_flesh
           - playeffect location:<%value%.location> effect:RED_DUST qty:1000
           - hurt 10000 %value%
           - heal 1000 <npc>
           - flag %value% Golermagerus_Color:!
           - flag %value% Golermagerus_Bomb:!
           }
         - if <%value%.flag[Golermagerus_Color]> == "green" {
           - narrate targets:%value% "<aqua>You have failed to remove your color assignment!"
           - announce "<red><%value%.name> failed to remove their color assignment and healed the boss for 1000 HP!"
           - playsound location:<%value%.location> sound:hurt_flesh
           - playeffect location:<%value%.location> effect:RED_DUST qty:1000
           - hurt 10000 %value%
           - heal 1000 <npc>
           - flag %value% Golermagerus_Color:!
           - flag %value% Golermagerus_Bomb:!
           }
         - random 2
         - flag %value% "Golermagerus_Color:green" duration:300
         - flag %value% "Golermagerus_Color:red" duration:300
         - flag %value% "Golermagerus_Ability_GCD:ON" duration:2
         - playsound location:<player.location> sound:ORB_PICKUP pitch:0
         - if <%value%.flag[Golermagerus_Color]> == "red" {
           - firework <%value%.location> power:0 primary:red flicker trail type:ball_large
           - narrate targets:%value% "<&D><npc.name> has colored you <red>Red<&D>!" }
         - if <%value%.flag[Golermagerus_Color]> == "green" {
           - firework <%value%.location> power:0 primary:green flicker trail type:ball_large
           - narrate targets:%value% "<&D><npc.name> has colored you <green>Green<&D>!" }
       }
       
"Golermagerus_GreenPlatform_Event":
   type: world
   events:
     on walked over GOLERMAG_GREEN_1:
       - if <player.flag[Golermagerus_Color]> == "green" {
         - narrate targets:player "<aqua>You have successfully cleared your color assignment!"
         - announce "<aqua><player.name> removed their color assignment and harmed Golermagerus for 100 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 100 <n@Golermagerus>
         - flag <player> Golermagerus_Color:!
         }
     on walked over GOLERMAG_GREEN_2:
       - if <player.flag[Golermagerus_Color]> == "green" {
         - narrate targets:player "<aqua>You have successfully cleared your color assignment!"
         - announce "<aqua><player.name> removed their color assignment and harmed Golermagerus for 100 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 100 <n@Golermagerus>
         - flag <player> Golermagerus_Color:!
         }
     on walked over GOLERMAG_GREEN_3:
       - if <player.flag[Golermagerus_Color]> == "green" {
         - narrate targets:player "<aqua>You have successfully cleared your color assignment!"
         - announce "<aqua><player.name> removed their color assignment and harmed Golermagerus for 100 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 100 <n@Golermagerus>
         - flag <player> Golermagerus_Color:!
         }
     on walked over GOLERMAG_GREEN_4:
       - if <player.flag[Golermagerus_Color]> == "green" {
         - narrate targets:player "<aqua>You have successfully cleared your color assignment!"
         - announce "<aqua><player.name> removed their color assignment and harmed Golermagerus for 100 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 100 <n@Golermagerus>
         - flag <player> Golermagerus_Color:!
         }
       
"Golermagerus_RedPlatform_Event":
   type: world
   events:
     on walked over GOLERMAG_RED_1:
       - if <player.flag[Golermagerus_Color]> == "red" {
         - narrate targets:player "<aqua>You have successfully cleared your color assignment!"
         - announce "<aqua><player.name> removed their color assignment and harmed Golermagerus for 100 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 100 <n@Golermagerus>
         - flag <player> Golermagerus_Color:!
         }
     on walked over GOLERMAG_RED_2:
       - if <player.flag[Golermagerus_Color]> == "red" {
         - narrate targets:player "<aqua>You have successfully cleared your color assignment!"
         - announce "<aqua><player.name> removed their color assignment and harmed Golermagerus for 100 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 100 <n@Golermagerus>
         - flag <player> Golermagerus_Color:!
         }
     on walked over GOLERMAG_RED_3:
       - if <player.flag[Golermagerus_Color]> == "red" {
         - narrate targets:player "<aqua>You have successfully cleared your color assignment!"
         - announce "<aqua><player.name> removed their color assignment and harmed Golermagerus for 100 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 100 <n@Golermagerus>
         - flag <player> Golermagerus_Color:!
         }
     on walked over GOLERMAG_RED_4:
       - if <player.flag[Golermagerus_Color]> == "red" {
         - narrate targets:player "<aqua>You have successfully cleared your color assignment!"
         - announce "<aqua><player.name> removed their color assignment and harmed Golermagerus for 100 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 100 <n@Golermagerus>
         - flag <player> Golermagerus_Color:!
         }
       
"Golermagerus_DaBomb_Ability":
   type: task
   # This ability flags a player as the 'bomb', once every 10 seconds.
   # See the initial script comments at the top of this file for details.
   script:
     - if <npc.flag[Golermagerus_Bomb_CD]> == null {
       - flag npc "Golermagerus_Bomb_CD:true" duration:10
       - if <npc.flag[Golermagerus_BombGrace_CD]> != null queue clear
       - if <npc.flag[Golermagerus_BombGrace_CD]> == null flag npc "Golermagerus_BombGrace_CD:true" duration:4
       - foreach <<npc.location>.find.players.within[40].aslist> {
         - if <%value%.flag[Golermagerus_Ability_GCD]> == "ON" queue clear
         - if <%value%.flag[Golermagerus_Bomb]> == "YES" {
           - define badPlayer %value%
           - narrate targets:%badPlayer% "<aqua>You have failed to remove your bomb!"
           - announce "<red><%badPlayer%.name> failed to remove their bomb!"
           - foreach <<%badPlayer%.location>.find.players.within[40].aslist> {
             - narrate targets:%value% "<aqua><%badPlayer%.name><&sq>s bomb detonated and killed you!"
             - playsound location:<%value%.location> sound:hurt_flesh
             - playsound location:<%value%.location> sound:explode
             - playeffect location:<%value%.location> effect:RED_DUST qty:1000
             - playeffect location:<%value%.location> effect:explode qty:500
             - playeffect location:<%value%.location> effect:huge_explosion qty:1
             - hurt 10000 %value%
             - heal 10000 <npc>
             - flag %value% Golermagerus_Color:!
             - flag %value% Golermagerus_Bomb:!
             }
           - flag %badPlayer% Golermagerus_Bomb:!
           - flag %badPlayer% Golermagerus_Color:!
           }
         }
       - playsound location:<player.location> sound:CREEPER_HISS
       - playsound location:<player.location> sound:WITHER_SPAWN
       - firework <%value%.location> power:0 primary:purple flicker trail type:ball_large
       - announce "<red><npc.name> yells<&cm> <&sq><cons:bomb_msg><&sq>"
       - narrate "<&D>* * * YOU ARE THE BOMB! * * *"
       - flag player "Golermagerus_Bomb:YES" duration:300
       - flag %value% "Golermagerus_Ability_GCD:ON" duration:2
       }
       
"Golermagerus_PurplePlatform_Event":
   type: world
   events:
     on walked over GOLERMAG_PURPLE_1:
       - if <player.flag[Golermagerus_Bomb]> == "YES" {
         - narrate targets:player "<aqua>You have successfully removed your bomb!"
         - announce "<aqua><player.name> removed their bomb and harmed Golermagerus for 200 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 200 <n@Golermagerus>
         - flag <player> Golermagerus_Bomb:!
         }
     on walked over GOLERMAG_PURPLE_2:
       - if <player.flag[Golermagerus_Bomb]> == "YES" {
         - narrate targets:player "<aqua>You have successfully removed your bomb!"
         - announce "<aqua><player.name> removed their bomb and harmed Golermagerus for 200 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 200 <n@Golermagerus>
         - flag <player> Golermagerus_Bomb:!
         }
     on walked over GOLERMAG_PURPLE_3:
       - if <player.flag[Golermagerus_Bomb]> == "YES" {
         - narrate targets:player "<aqua>You have successfully removed your bomb!"
         - announce "<aqua><player.name> removed their bomb and harmed Golermagerus for 200 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 200 <n@Golermagerus>
         - flag <player> Golermagerus_Bomb:!
         }
     on walked over GOLERMAG_PURPLE_4:
       - if <player.flag[Golermagerus_Bomb]> == "YES" {
         - narrate targets:player "<aqua>You have successfully removed your bomb!"
         - announce "<aqua><player.name> removed their bomb and harmed Golermagerus for 200 damage!"
         - playsound location:<<n@Golermagerus>.location> sound:zombie_hurt pitch:0.7
         - playeffect location:<<n@Golermagerus>.location> effect:RED_DUST qty:1000
         - hurt 200 <n@Golermagerus>
         - flag <player> Golermagerus_Bomb:!
         }
       
"Golermagerus_initiate_Phase2":
   type: task
   # This ability starts phase 2.
   script:
     - teleport <npc> "location:<cons:Golermagerus_SpawnPoint>"
     - ^if <npc.health.percentage.as_int> <= 49 && <npc.flag[Golermagerus_BurnPhase_Active]> == null {
       - flag <npc> "Golermagerus_BurnPhase_Active:YES"
       - flag npc "Golermagerus_HarmPhase_Amt:1"
       - announce "<red><npc.name> yells<&cm> <&sq><cons:burnPhaseMsg><&sq>"
       - playsound location:<npc.location> sound:zombie_idle pitch:0.7
       }
     
"Golermagerus_Phase2_HarmCompounder":
   type: task
   # This ability does more damage over time. See the initial
   # script documentation at the top of this file for details.
   script:
     - teleport <npc> "location:<cons:Golermagerus_SpawnPoint>"
     - if <npc.flag[Golermagerus_Harm_CD]> == null {
       - flag npc "Golermagerus_Harm_CD:true" duration:15
       - announce "<red><npc.name> yells<&cm> <&sq><cons:burnPhaseHarm><&sq>"
       - define %dmgAmt% <math.as_int:<npc.flag[Golermagerus_HarmPhase_Amt]>*2>
       - foreach <<npc.location>.find.players.within[40].aslist> {
         - narrate targets:%value% "<&9><npc.name><&sq>s Surge of Energy hits your for %dmgAmt% damage."
         - hurt %dmgAmt% %value%
         }
       - flag npc Golermagerus_HarmPhase_Amt:++
       }
       
"GolermagerusPlayerDied":
  type: world
  events:
    on player death:
    - flag <player> Golermagerus_Color:!
    - flag <player> Golermagerus_Bomb:!
       
# -----------------------------------
# Loot Items
"Artemis":
   type: item
   material: 261
   display name: "<&5>Artemis"
   lore:
   - <white>Deadly. Like<&cm> really deadly.
   enchantments:
   - arrow_damage:10
   - durability:10
 
"Mithaniel":
   type: item
   material: 276
   display name: "<&5>Mithaniel"
   lore:
   - <white>This blade is made of reforged Mithril
   - <white>and blessed by the Wood Elves.
   enchantments:
   - damage_all:10
   - durability:10
   
"Golem_Helm":
   type: item
   material: 310
   display name: "<&5>Golem Helmet"
   lore:
   - <white>A helmet forged by the spare
   - <white>parts of a titanium golem.
   enchantments:
   - durability:12
   - oxygen:5
   
"Golem_Chest":
   type: item
   material: 311
   display name: "<&5>Golem Breastplate"
   lore:
   - <white>A breastplate forged by the spare
   - <white>parts of a titanium golem.
   enchantments:
   - durability:12
   - protection_environmental:12
         
"Golem_Legs":
   type: item
   material: 312
   display name: "<&5>Golem Greaves"
   lore:
   - <white>A pair of greaves forged by the spare
   - <white>parts of a titanium golem.
   enchantments:
   - durability:12
   - protection_fire:12
         
"Golem_Boots":
   type: item
   material: 313
   display name: "<&5>Golem Boots"
   lore:
   - <white>A pair of boots forged by the spare
   - <white>parts of a titanium golem.
   enchantments:
   - durability:12
   - protection_fall:10