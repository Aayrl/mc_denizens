# -----------------------------------
# Lady Sinestra Boss Script
# @author: Tyler J. Sawyer
# @version: 1.3
# Last Updated: 13:17 on May 16, 2014 (Friday)
# -----------------------------------
# Lady Sinestra is a boss encounter in the Plane of Ender.
#
# Respawn Timer : 86400 seconds - 24 hours
#
# Phase 1 : 98% -> 75%
# ABILITY: Ignite : Burns a random target for 10 seconds. [30s Cooldown]
# ABILITY: Lava Dredge : Harm a random player for 50% of their health. [30s Cooldown]
#
# Phase 2 : 74% -> 50%
# ADDS [60s Cooldown]
#   Spawn 1 Large Cube in each corner of the area.
#   "Sinestra Spawn"
#   - 250 Health
#   - Shoot Fireballs
#   On Death:
#       Spawn 2 Smaller Cubes
#       "Sinestra-ling"
#       - 100 Health
#       - Shoot Fireballs
#
# Phase 3 : 49% -> 25%
# ABILITY: Lava Geyser : Burns a random target for 10 seconds.
#                        "Bounces" the target in the air [30s Cooldown]
# ABILITY: Fear : Turns all targets away from the boss for 5 seconds. 
#               : Also blinds and roots the player. [45s Cooldown]
#
# Phase 4 : 24% -> 5%
# ABILITY: Call of the Nest : Teleport all players to the nest (spawn point) and root for 10 sec. [20s Cooldown]
# -----------------------------------

ladysinestra:
  type: assignment

  default constants:
    agro message: I must protect these eggs at all costs!
    death message: My Lord! I have failed you!
    victory message: None should challenge the Dragonkin!
    respawn message: Quickly my servants<&cm> there are mortals afoot! Protect the eggs!
    
    # Special Event Messages
    ignite_msg: Let us see how impervious to flame these mortals are!
    lavadredge_msg: How does it feel to be bathed in molten lava?
    adds_msg: Servants! To my side<&cm> strike our foes!
    lavageyser_msg: A touch of liquid fire should take care of you!
    fear_msg: Flee! Flee and never return!
    callofnest_msg: Get over here and face the wrath of Sinestra!

    # Sinestra : Statistics
    # Sinestra Health Pool
    sinestra_health: 7500
    # Sinestra Damage / Swing
    sinestra_str: 3
    # Sinestra Armor Value
    sinestra_AC: 1
    # Sinestra Agro Radius (in Blocks)
    sinestra_AGRO_RADIUS: 15
    # Sinestra Movement Speed (Blocks / Tick)
    sinestra_Movement_Speed: 2
    # Sinestra's Spawnpoint
    sinestra_SpawnPoint: "34,46,-97,world_the_end"
    # Sinestra Respawn Timer in seconds (43200 = 12 hrs)
    sinestra_Respawn_Timer: 86400
    # Sinestra XP Amount dropped on death
    sinestra_XP_AMT: 7500

  interact scripts:
  - 10 sinestraScript

  actions:
    on assignment:
    - trigger name:proximity toggle:true "radius:<cons:sinestra_AGRO_RADIUS>"
    - trigger name:damage toggle:true

    # Setup Sinestra stats.
    - execute as_server "npc sel <npc.id>"
    - execute as_server "npc type MAGMA_CUBE"
    - execute as_server "npc speed <cons:sinestra_Movement_Speed>"
    - execute as_server "npc lookclose"
    - execute as_server "trait sentry"
    - execute as_server "sentry health <cons:sinestra_health>"
    - execute as_server "sentry respawn <cons:sinestra_Respawn_Timer>"
    - execute as_server "sentry speed <cons:sinestra_Movement_Speed>"
    - execute as_server "sentry strength <cons:sinestra_str>"
    - execute as_server "sentry armor <cons:sinestra_AC>"
    - execute as_server "sentry range <cons:sinestra_AGRO_RADIUS>"

    on spawn:
    - announce "<red><npc.name> yells<&cm> <&sq><cons:respawn message><&sq>"
    # Warp boss to its spawnpoint
    - teleport <npc> "location:<cons:sinestra_SpawnPoint>"
    # Clear previous targets from hate list
    - execute as_server "npc sel <npc.id>"
    - execute as_server "sentry target clear"
    - execute as_server "sentry target add entity:player"
    # Clear boss flags
    - flag npc BossPlayerList:!
    - flag npc sinestra_ignite_CD:!
    - flag npc sinestra_lavadredge_CD:!
    - flag npc sinestra_adds_CD:!
    - flag npc sinestra_lavageyser_CD:!
    - flag npc sinestra_fear_CD:!
    - flag npc sinestra_callofnest_CD:!

    on death:
    # XP amount
    - drop XP qty:<cons:sinestra_XP_AMT> location:<npc.location>
    # Drop the left bindings 100% of the time.
    - drop "TF_BindingLeft" qty:1 location:<npc.location>
    # Drop an enderdragon ore 100% of the time.
    - drop "EnderdragonOre" qty:1 location:<npc.location>
    # Drop the special drake egg 10% of the time.
    #- drop "sinestraEggItem" qty:1 location:<npc.location>
    # Random Loot Item 1
    #- random 6
    #- drop "Hate Harvester" qty:1 location:<npc.location>
    #- drop "Amulet of Hate" qty:1 location:<npc.location>
    #- drop "Face of Death" qty:1 location:<npc.location>
    #- drop "Orb of Sinestra" qty:1 location:<npc.location>
    #- drop "Skyfall" qty:1 location:<npc.location>
    #- drop "Death Lash" qty:1 location:<npc.location>
    # Random Loot Item 2
    #- random 6
    #- drop "Hate Harvester" qty:1 location:<npc.location>
    #- drop "Amulet of Hate" qty:1 location:<npc.location>
    #- drop "Face of Death" qty:1 location:<npc.location>
    #- drop "Orb of Sinestra" qty:1 location:<npc.location>
    #- drop "Skyfall" qty:1 location:<npc.location>
    #- drop "Death Lash" qty:1 location:<npc.location>
    # Reset npc effects and clear hate targets.
    - execute as_server "npc sel <npc.id>"
    - execute as_server "npc effect --play none"
    - execute as_server "sentry target clear"
    # Announce to players the boss is dead.
    - announce "<red><npc.name> yells, <&sq><cons:death message><&sq>"
    - announce "<&5><npc.name> has been slain!"
    - foreach <npc.flag[BossPlayerList].aslist> {
      - adjust <player> "show_boss_bar:"
      - flag player:%value% sinestra_Engaged:!
      - flag player:%value% "ENDER_sinestraKills:++"
      - flag player:%value% LBOARD_B:++
      - flag player:%value% LBOARD_SCORE_TOTAL:<math.as_int:<<player>.flag[LBOARD_SCORE_TOTAL]>+100>
      - narrate targets:<%value%.name> "<aqua>You assisted in slaying <npc.name>."
      }

    on enter proximity:
    - announce "<red><npc.name> yells<&cm> <&sq><cons:agro message><&sq>"
    - playsound location:<npc.location> sound:blaze_breath pitch:-50
    # Flag the player as 'engaged' with the boss for 30 seconds.
    # If the player attacks the NPC, this refreshes.
    # If the player gets summoned, this refreshes.
    # If the player warps to spawn or home, this duration will fade out.
    - flag player "sinestra_Engaged:true" duration:30
    # Adjust BossBar Health
    - adjust <player> "show_boss_bar:<m.as_int:<npc.health.percentage.as_int>*2>|<npc.name> - <npc.health.percentage.as_int>%"

    on exit proximity:
    # summon player if they are still engaged by the boss.
    - if "<flag.p:sinestra_Engaged:true || false>" == true {
      - announce "<red><npc.name> yells<&cm> <&sq>You will not evade me<&cm> <player.name>!<&sq>"
      - narrate "<yellow>You have been summoned!"
      - teleport <player> "location:<npc.location>"
      - playsound location:<player.location> sound:portal_travel
      - playeffect location:<player.location> effect:portal qty:1000
      }
    - adjust <player> "show_boss_bar:"
    
    on kill:
    - announce "<red><npc.name> yells<&cm> <&sq><cons:victory message><&sq>"

"sinestraScript":
   type: interact
   steps:
     1:
       click trigger:
         script:
         # Right Clicking on the boss tells you his % health.
         - narrate "<yellow>Lady Sinestra<&co> <npc.health.percentage.as_int>%"
       proximity trigger:
         entry:
           script:
           # Add player to the boss list when they enter his proximity.
           - if <npc.flag[BossPlayerList].aslist> !contains <player> && <player.name> != <npc.name> flag npc BossPlayerList:->:<player>
       damage trigger:
         script:
           # When the boss is hit, re-flag the player as engaged with the boss.
           - flag player "sinestra_Engaged:true" duration:30
           - playsound location:<npc.location> sound:donkey_hit pitch:-10
           - playeffect location:<npc.location> effect:lava qty:15
           - playeffect location:<npc.location> effect:mobspawner_flames qty:1
           # Adjust BossBar Health
           - if <npc.health.percentage.as_int> == null {
             - adjust <player> "show_boss_bar:"
             }
             else {
             - adjust <player> "show_boss_bar:<m.as_int:<npc.health.percentage.as_int>*2>|<npc.name> - <npc.health.percentage.as_int>%"
             }
           # Here are the boss special events that fire off throughout the fight.
           - ^if <npc.health.percentage.as_int> <= 98 {
             - random 2
             - run "sinestra_ignite"
             - run "sinestra_lavadredge"
             }
           - ^if <npc.health.percentage.as_int> <= 74 {
             - run "sinestra_adds"
             }
           - ^if <npc.health.percentage.as_int> <= 49 {
             - random 2
             - run "sinestra_lavageyser"
             - run "sinestra_fear"
             }
           - ^if <npc.health.percentage.as_int> <= 74 {
             - run "sinestra_callofnest"
             }

"sinestra_ignite":
   type: task
   # This sets a player on fire for 10 seconds every 30 seconds.
   script:
     - if <npc.flag[sinestra_ignite_CD]> == null {
       - flag npc "sinestra_ignite_CD:true" duration:30
       - announce "<red><npc.name> yells<&cm> <&sq><cons:ignite_msg><&sq>"
       - narrate "<&9>You are set ablaze by <npc.name>!"
       - burn <player> duration:10s
       - playsound explode location:<player.location>
       - playeffect location:<player.location> effect:lava qty:100
       }
       
"sinestra_lavadredge":
   type: task
   # This ability harms a random player for 20 health.
   script:
     - if <npc.flag[sinestra_lavadredge_CD]> == null {
       - flag npc "sinestra_lavadredge_CD:true" duration:30
       - announce "<red><npc.name> yells<&cm> <&sq><cons:lavadredge_msg><&sq>"
       - narrate "<&9><npc.name><&sq>s Lava Dredge hits you for 20 damage."
       - hurt 20 <player>
       - playeffect location:<player.location> effect:mobspawner_flames qty:50
       }
       
"sinestra_lavageyser":
   type: task
   # This ability bounces a player in the air, harming them for a total 
   # of 20 damage. The player is also set on fire.
   script:
     - if <npc.flag[sinestra_lavageyser_CD]> == null {
       - flag npc "sinestra_lavageyser_CD:true" duration:30
       - announce "<red><npc.name> yells<&cm> <&sq><cons:lavageyser_msg><&sq>"
       - narrate "<&9><npc.name> summons a Lava Geyser<&cm> tossing you wildly into the air!"
       - narrate "<&9>You are set ablaze by <npc.name>!"
       - burn <player> duration:10s
       - teleport "<player.location.x>,<m:<player.location.y>+2.0>,<player.location.z>,world_the_end"
       - hurt 4 <player>
       - playeffect location:<player.location> effect:mobspawner_flames qty:25
       - playeffect location:<player.location> effect:lava qty:100
       - narrate "<&9><npc.name><&sq>s Lava Geyser burns you for 4 damage."
       - teleport "<player.location.x>,<m:<player.location.y>+2.0>,<player.location.z>,world_the_end"
       - hurt 4 <player>
       - playeffect location:<player.location> effect:mobspawner_flames qty:25
       - playeffect location:<player.location> effect:lava qty:100
       - narrate "<&9><npc.name><&sq>s Lava Geyser burns you for 4 damage."
       - teleport "<player.location.x>,<m:<player.location.y>+2.0>,<player.location.z>,world_the_end"
       - hurt 4 <player>
       - playeffect location:<player.location> effect:mobspawner_flames qty:25
       - playeffect location:<player.location> effect:lava qty:100
       - narrate "<&9><npc.name><&sq>s Lava Geyser burns you for 4 damage."
       - teleport "<player.location.x>,<m:<player.location.y>+2.0>,<player.location.z>,world_the_end"
       - hurt 4 <player>
       - playeffect location:<player.location> effect:mobspawner_flames qty:25
       - playeffect location:<player.location> effect:lava qty:100
       - narrate "<&9><npc.name><&sq>s Lava Geyser burns you for 4 damage."
       - teleport "<player.location.x>,<m:<player.location.y>+2.0>,<player.location.z>,world_the_end"
       - hurt 4 <player>
       - playeffect location:<player.location> effect:mobspawner_flames qty:25
       - playeffect location:<player.location> effect:lava qty:100
       - narrate "<&9><npc.name><&sq>s Lava Geyser burns you for 4 damage."
       }

"sinestra_fear":
   type: task
   # This ability 'fears' all players, rooting them and blinding them for 5 sec.
   script:
     - if <npc.flag[sinestra_fear_CD]> == null {
       - flag npc "sinestra_fear_CD:true" duration:45
       - announce "<red><npc.name> yells<&cm> <&sq><cons:fear_msg><&sq>"
       - foreach <npc.location.find.players.within[30].aslist> {
         - narrate targets:%value% "<&9>You flee in terror!"
         - cast slow target:%value% duration:5 power:10
         - cast blindness target:%value% duration:5s power:50
         - playeffect location:<%value%.location> effect:portal qty:1000
         }
       }
       
"sinestra_callofnest":
   type: task
   # This ability summons all players to the boss' spawn point,
   # rooting them for 10 seconds.
   script:
     - if <npc.flag[sinestra_callofnest_CD]> == null {
       - flag npc "sinestra_callofnest_CD:true" duration:20
       - announce "<red><npc.name> yells<&cm> <&sq><cons:callofnest_msg><&sq>"
       - foreach <npc.location.find.players.within[30].aslist> {
         - teleport %value% "location:<cons:sinestra_SpawnPoint>"
         - narrate targets:%value% "<&9>You are summoned to <npc.name><&sq>s nest!"
         - cast slow target:%value% duration:10 power:10
         - playeffect location:<%value%.location> effect:portal qty:1000
         }
       }
      
# -----------------------------------
# ADDS SCRIPT
"sinestra_adds":
   type: task
   # This ability summons four adds, one in each corner of the room.
   script:
     - if <npc.flag[sinestra_adds_CD]> == null {
       - flag npc "sinestra_adds_CD:true" duration:60
       - announce "<red><npc.name> yells<&cm> <&sq><cons:adds_msg><&sq>"
       }

# -----------------------------------
# Loot Items