# ========================================
# Blacksmith Script
# Tyler J. Sawyer
# Last Updated: 01:48 on May 11, 2014 (Sunday)
# ========================================
# 
# Blacksmith Script
# ----------------
#
# The Blacksmith Script allows administrators to assign NPCs the role of
# blacksmith. NOTE: This is not the same as the citizens2 trait 'blacksmith'
# 
# To use: Place the desired item in your hand and right click on the NPC.
# The NPC will then determine how many credits it will cost for you to repair
# that item, and will the prompt you to confirm in chat that you wish to
# repair the item. Upon confirmation, the script will deduct the desired
# amount of credits from the player's wallet and repair the item in their
# hand to full.
#
# Based on some script mechanics from: 
# http://scripts.citizensnpcs.co/view/b6fe1z
# ========================================
      
"DurabilityCommand":
  type: world
  events:
    # Durability Command
    # Tell the player the current durability of the item
    # in their hand.
    on durability command:
    - if <<player.item_in_hand>.repairable> == true {
      - if <player.item_in_hand.display> == null {
        - narrate "<yellow><player.item_in_hand.material.formatted> Durability<&co> <math.as_int:<<player.item_in_hand.max_durability>-<player.item_in_hand.durability>>> / <player.item_in_hand.max_durability>."
        }
      - if <player.item_in_hand.display> != null {
        - narrate "<yellow><player.item_in_hand.display> Durability<&co> <math.as_int:<<player.item_in_hand.max_durability>-<player.item_in_hand.durability>>> / <player.item_in_hand.max_durability>."
        }
    - if <<player.item_in_hand>.repairable> == false {
      - if <player.item_in_hand.display> == null {
        - narrate "<red><player.item_in_hand.material.formatted> is not affected by durability!"
        }
      - if <player.item_in_hand.display> != null {
        - narrate "<red>Your <player.item_in_hand.display> is not affected by durability!"
        }
      }
    # Smithhelp Command
    # Tell the player the current durability of the item
    # in their hand.
    on smithhelp command:
    - narrate "<yellow>---- Blacksmith Help ----"
    - narrate "<yellow><green>Step 1<yellow><&co> Place an item you wish to repair in your hand."
    - narrate "<yellow><green>Step 2<yellow><&co> Right Click on the Smithy to appraise the repair cost for the item of interest."
    - narrate "<yellow><green>Step 3<yellow><&co> The Smithy will tell you how many credits it will cost to repair the item."
    - narrate "<yellow><green>Step 4<yellow><&co> To repair the item<&cm> simply respond to the Smithy by typing <red>repair<yellow> into chat."
      
# ============================================================================================
#

# Blacksmith Assignment
"blacksmith_custom":
    type: assignment
    
    default constants:
        # Costs to repair one point of durability
        repairdurability: 5
        # Costs to repair each level of enchant
        repairenchant: 150
    
    interact scripts:
    - 10 blacksmith_customScript
    
    actions:
      on spawn:
      - trigger name:proximity toggle:true radius:4
      
      on enter proximity:
      - narrate "<white><npc.name> says<&cm> <&sq>How are you<&cm> <player.name>? Need some equipment repaired?<&sq>"
      - narrate "<yellow>Type <gold>/smithhelp<yellow> for assistance using Blacksmiths."
      
      on exit proximity:
      - narrate "<white><npc.name> says<&cm> <&sq>Safe travels<&cm> <player.name>.<&sq>"
      - zap 's@blacksmith_customScript' step:default

'blacksmith_customScript':
    type: interact
    
    steps:
      default:
        click trigger:
          script:
            # Right Clicking on the blacksmith appraises the item in the player's hand.
            - if <<player.item_in_hand>.repairable> == true {
              - if <<player.item_in_hand>.durability> == 0 {
                - narrate "<white><npc.name> says<&cm> <&sq>That item is already fully repaired<&cm> <player.name>.<&sq>"
                - queue clear
                }
              - run determineRepairCost instantly
              - if <flag.p:REPAIR_repaircost.as_int> == 0 {
                - narrate "<white><npc.name> says<&cm> <&sq>Hrm<&cm> did you accidentally equip your item? I cannot seem to find it<&cm> <player.name>.<&sq>"
                - queue clear
                }
              - narrate "<white><npc.name> says<&cm> <&sq>It will cost <yellow><flag.p:REPAIR_repaircost.as_int><white> credits to repair that item<&cm> <player.name>.<&sq>"
              - narrate "<white><npc.name> says<&cm> <&sq>Do you wish to <&lb><red>repair<white><&rb> this item for <flag.p:REPAIR_repaircost.as_int> credits?<&sq>"
              - zap repairStep
              }
              else {
              - narrate "<white><npc.name> says<&cm> <&sq>That item cannot be repaired<&cm> <player.name>.<&sq>"
              }
      repairStep:
        chat trigger:
          'repairMe':
            trigger: 'I would like you to /repair/ my item.'
            script:
              - if <player.flag[CURRENCY_Credits]> >= <flag.p:REPAIR_repaircost> {
                - narrate "<white><npc.name> says<&cm> <&sq>Please wait while I repair your item...<&sq>"
                - playsound <player.location> sound:anvil_use
                - define creditAmt <flag.p:REPAIR_repaircost.as_int>
                - define currentAmt <player.flag[CURRENCY_Credits].as_int>
                - define newAmt <math.as_int:%currentAmt%-%creditAmt%>
                - flag <player> "CURRENCY_Credits:%newAmt%"
                - adjust <player.item_in_hand> durability:0 save:repairitemtemp
                - take iteminhand
                - give <entry[repairitemtemp].result>
                - narrate "<white><npc.name> says<&cm> <&sq>There<&cm> good as new<&cm> <player.name>!<&sq>"
                - narrate "<yellow>You pay <npc.name> %creditAmt% credits to repair your item."
                - narrate "<yellow>You now have <player.flag[CURRENCY_Credits]> credits."
                - zap default
                }
                else {
                - narrate "<white><npc.name> says<&cm> <&sq>Sorry <player.name><&cm> but you do not have enough credits to repair that item.<&sq>"
                - zap default
                }
                
"determineRepairCost":
    type: task
    script:
        # Check for enchantments and the state of durability used.
        - if <<player.item_in_hand>.enchantments> != null {
            - flag player REPAIR_enchantlvl:0
            - foreach <<player.item_in_hand>.enchantments.levels> {
                - flag player REPAIR_enchantlvl:++:%value%
              }
          }
          else {
              - flag player REPAIR_enchantlvl:0
          }
        - flag player REPAIR_duralvl:<<player.item_in_hand>.durability>

        # Calculates repair cost based on enchant level & durability.
        - flag player REPAIR_enchantcost:<flag.p:REPAIR_enchantlvl>
        - flag player REPAIR_enchantcost:**:<cons:repairenchant>

        - flag player REPAIR_duracost:<flag.p:REPAIR_duralvl>
        - flag player REPAIR_duracost:**:<cons:repairdurability>

        - flag player REPAIR_repaircost:<flag.p:REPAIR_enchantcost>
        - flag player REPAIR_repaircost:++:<flag.p:REPAIR_duracost>