# -----------------------------------
# Leaderboard
# @author: Tyler J. Sawyer
# @version: 1.0
# Last Updated: 14:04 on May 06, 2014 (Tuesday)
# -----------------------------------
# This script tracks players progress throughout the game
# and generates a 'score' based on their progress.
# 
# Players earn a score in each of the following categories:
# - 5 points per monster kill
# - 100 points per boss kill
# - 25 points per quest complete
# - 10 points per pvp kill
# - 1 point per block break
# - 1 point per block place

# If the player has no scores, initialize all of them to 0.
"initLBoardScores_PerPlayer":
  type: world
  events:
    on player join:
    - if <player.flag[LBOARD_SCORE_TOTAL]> == "null" {
      - flag player "LBOARD_SCORE_TOTAL:0" }
    - if <player.flag[LBOARD_RANK]> == "null" {
      - flag player "LBOARD_RANK:0" }
    - if <player.flag[LBOARD_M]> == "null" {
      - flag player "LBOARD_M:0" }
    - if <player.flag[LBOARD_B]> == "null" {
      - flag player "LBOARD_B:0" }
    - if <player.flag[LBOARD_Q]> == "null" {
      - flag player "LBOARD_Q:0" }
    - if <player.flag[LBOARD_PVP]> == "null" {
      - flag player "LBOARD_PVP:0" }
    - if <player.flag[LBOARD_BLOCK]> == "null" {
      - flag player "LBOARD_BLOCK:0" }
    - if <player.flag[LBOARD_BUILD]> == "null" {
      - flag player "LBOARD_BUILD:0" }
      
# Leaderboard Commands
# Check total leaderboard rankings
# TO DO: Allow players to type /stats <player> to see other player stats.
"LEADERBOARD_ServerRanks":
  type: world
  events:
    on leaderboard command:
    - flag server LEADERBOARD_PLAYERS:!
    - narrate "<aqua>--- Leaderboard Rankings ---"
    - foreach <server.list_players.aslist> {
      - define currentPlayer %value%
      - if <server.flag[LEADERBOARD_PLAYERS].aslist> !contains %currentPlayer% {
        - flag server LEADERBOARD_PLAYERS:->:%currentPlayer%
        - flag %currentPlayer% "LBOARD_RANK:1"
        - if <%currentPlayer%.flag[LBOARD_SCORE_TOTAL]> == "null" define score 0
        - if <%currentPlayer%.flag[LBOARD_SCORE_TOTAL]> != "null" define score <%currentPlayer%.flag[LBOARD_SCORE_TOTAL]>
        - foreach <server.flag[LEADERBOARD_PLAYERS].aslist> {
          - if <%value%.flag[LBOARD_SCORE_TOTAL]> == "null" flag %value% LBOARD_SCORE_TOTAL:0
          - if <%value%.flag[LBOARD_SCORE_TOTAL]> != "null" define checkScore <%value%.flag[LBOARD_SCORE_TOTAL]>
          - if %checkScore% > %score% {
            - flag %currentPlayer% LBOARD_RANK:++
            }
          - if %checkScore% < %score% {
            - flag %value% LBOARD_RANK:++
            }
          }
        }
      }
    - foreach <server.flag[LEADERBOARD_PLAYERS].aslist> {
      - define rank <%value%.flag[LBOARD_RANK].as_int>
      - define score <%value%.flag[LBOARD_SCORE_TOTAL].as_int>
      - define name <%value%.name>
      - define online ""
      - if <%value%.is_online> define online "<green>"
      - if <%value%.is_op> define online "<dark_purple>"
      - if %score% > 0 narrate "<aqua><&lb><gold>%rank%.<aqua><&rb> %online% %name% <aqua>-- <green>%score%"
      }
        
    on stats command:
    - if <context.args.get[1]> == null {
      - narrate "<aqua>--- Your Leaderboard Statistics ---"
      - narrate "<gold>Your Rank<&co> <player.flag[LBOARD_RANK]>"
      - narrate "<aqua>Total Score<&co> <green><player.flag[LBOARD_SCORE_TOTAL]>"
      - narrate "<aqua>Monster Kills<&co> <green><player.flag[LBOARD_M]>"
      - narrate "<aqua>Boss Kills<&co> <green><player.flag[LBOARD_B]>"
      - narrate "<aqua>Player Kills<&co> <green><player.flag[LBOARD_PVP]>"
      - narrate "<aqua>Quest Completes<&co> <green><player.flag[LBOARD_Q]>"
      - narrate "<aqua>Block Breaks<&co> <green><player.flag[LBOARD_BLOCK]>"
      - narrate "<aqua>Blocks Placed<&co> <green><player.flag[LBOARD_BUILD]>"
      - narrate "<aqua>--------------------------------"
      }
    - if <context.args.get[1]> != null {
      - define playerQuery <context.args.get[1]>
      - if <server.list_players.aslist> !contains <p@%playerQuery> {
        - narrate "<red>%playerQuery% has not played on this server or does not exist."
        - queue clear
        }
      - narrate "<aqua>--- %playerQuery%<&sq>s Leaderboard Statistics ---"
      - narrate "<gold>%playerQuery%<&sq>s Rank<&co> <<p@%playerQuery%>.flag[LBOARD_RANK]>"
      - narrate "<aqua>Total Score<&co> <green><<p@%playerQuery%>.flag[LBOARD_SCORE_TOTAL]>"
      - narrate "<aqua>Monster Kills<&co> <green><<p@%playerQuery%>.flag[LBOARD_M]>"
      - narrate "<aqua>Boss Kills<&co> <green><<p@%playerQuery%>.flag[LBOARD_B]>"
      - narrate "<aqua>Player Kills<&co> <green><<p@%playerQuery%>.flag[LBOARD_PVP]>"
      - narrate "<aqua>Quest Completes<&co> <green><<p@%playerQuery%>.flag[LBOARD_Q]>"
      - narrate "<aqua>Block Breaks<&co> <green><<p@%playerQuery%>.flag[LBOARD_BLOCK]>"
      - narrate "<aqua>Blocks Placed<&co> <green><<p@%playerQuery%>.flag[LBOARD_BUILD]>"
      - narrate "<aqua>--------------------------------"
      }
    on statsupdate command:
    - narrate "<red>Updating your Leaderboard Score...Please Wait..."
    - run s@LEADERBOARD_CalculatePlayerScore_Narrate d:<player>
    
    
# Leaderboard Calculate Player Score
# Calculates a given player's score.
#- run s@LEADERBOARD_CalculatePlayerScore_Narrate d:<player>
"LEADERBOARD_CalculatePlayerScore_Narrate":
  type: procedure
  definitions: playerObj
  script:
  - define monsterScore <math.as_int:<%playerObj%.flag[LBOARD_M]>*5>
  - define bossScore <math.as_int:<%playerObj%.flag[LBOARD_B]>*100>
  - define playerKillScore <math.as_int:<%playerObj%.flag[LBOARD_PVP]>*10>
  - define questScore <math.as_int:<%playerObj%.flag[LBOARD_Q]>*25>
  - define blockScore <%playerObj%.flag[LBOARD_BLOCK]>
  - define buildScore <%playerObj%.flag[LBOARD_BUILD]>
  - flag %playerObj% "LBOARD_SCORE_TOTAL:<math.as_int:%monsterScore%+%bossScore%+%playerKillScore%+%questScore%+%blockScore%+%buildScore%>"
  - narrate targets:%playerObj% "<red>Your scores have been updated."
  
    
# Leaderboard PvP Event
# Flag Players when other players are slain.
# Flag players when mobs are slain.
"LEADERBOARD_KILLCREDIT":
  type: world
  events:
    on player death:
    - if <context.damager.is_player> {
      - flag <context.damager> LBOARD_PVP:++
      - flag <context.damager> LBOARD_SCORE_TOTAL:<math.as_int:<<context.damager>.flag[LBOARD_SCORE_TOTAL]>+10>
      }
    on entity death:
    - if <context.damager.is_player> {
      - flag <context.damager> LBOARD_M:++
      - flag <context.damager> LBOARD_SCORE_TOTAL:<math.as_int:<<context.damager>.flag[LBOARD_SCORE_TOTAL]>+5>
      }

# Leaderboard Block Break Event
# Flag Players when they break a block, 
# or when they place a block.
"LEADERBOARD_BLOCKCREDIT":
  type: world
  events:
    on player breaks block:
    - flag <player> LBOARD_BLOCK:++
    - flag <player> LBOARD_SCORE_TOTAL:++
    on player places block:
    - flag <player> LBOARD_BUILD:++
    - flag <player> LBOARD_SCORE_TOTAL:++
