# Avatar of Hakkar is a boss fight.
# Tyler J. Sawyer
# Based loosely on : http://scripts.citizensnpcs.co/view/34aapf

# AOH
# on attack
# flag player inBossFight:true
# on player death:
# if flag player inboosfight == true
# retain player inventory

# AOH Fight
# Phase 1:
# 100 - 50%
# Knock back : 10 block teleport in air random player on hate list
# 30 sec cooldown
# Summon 2 zombie adds one of four corner locations randomly
# 30 sec cooldown
# Spalsh potion of poison on boss every 20 sec
# do splash potion effect on boss

# Phase 2:
# 49 - 0%
# All of phase 1 abilities
# Sacrifice:
#  TP random palyer to alter
# Poison and blind the player
# 30 sec cooldown

# Respawn Timer : 43200 seconds - 12 hours

aohakkar:
  type: assignment

  default constants:
    # Agro Message : Message to display when a player engages the boss.
    agro message: <red>You cannot stop the power of Hakkar!
    # Death Message : Message displayed when the boss is slain.
    death message: <red>-HISSSS- You have not seen the last of Hakkar!
    # Victory Message : Message when a player is killed by the boss.
    victory message: <red>Where is your god now<&cm> <player.name>?
    # Special Event Messages
    poisonAE_msg: <red>Choke on Hakkarian Blood!
    knockback_msg: <red>Get out of my way<&cm> <player.name>!
    summonAdds_msg: <red>Rise my minions<&cm> show our friends the power of Hakkar!
    sacrifice_msg: <red>You will make a good sacrifice<&cm> <player.name>!

    # Avatar of Hakkar : Statistics
    # Avatar of Hakkar Health Pool
    aoh_health: 2500
    # Avatar of Hakkar Damage / Swing
    aoh_str: 5
    # Avatar of Hakkar Armor Value
    aoh_AC: 1
    # Avatar of Hakkar Agro Radius (in Blocks)
    aoh_AGRO_RADIUS: 15
    # Avatar of Hakkar Movement Speed (Blocks / Tick)
    aoh_Movement_Speed: 2
    # Avatar of Hakkar's Spawnpoint
    aoh_SpawnPoint: "23,69,536,world"
    # Avatar of Hakkar Respawn Timer in seconds (43200 = 12 hrs)
    aoh_Respawn_Timer: 10
    # Avatar of Hakkar XP Amount he drops on death
    aoh_XP_AMT: 2500

    # Avatar of Hakkar : Adds
    # Avatar of Hakkar Add Spawn Point Sphere
    aoh_add_cuboid_1: "30,69,527,world"
    aoh_add_cuboid_2: "14,69,537,world"

  interact scripts:
  - 10 AOHakkarScript

  actions:
    on assignment:
    - trigger name:proximity toggle:true "radius:<cons:aoh_AGRO_RADIUS>"
    - trigger name:damage toggle:true

    # Avatar of Hakkar Cuboid for Add Spawns.
    - flag npc "cuboid:cu@<cons:aoh_add_cuboid_1>|<cons:aoh_add_cuboid_2>"

    # Setup Avatar of Hakkar stats.
    - execute as_server "npc sel <npc.id>"
    - execute as_server "npc type player"
    - execute as_server "npc speed <cons:aoh_Movement_Speed>"
    - execute as_server "npc lookclose"
    - execute as_server "trait sentry"
    - execute as_server "sentry health <cons:aoh_health>"
    - execute as_server "sentry respawn <cons:aoh_Respawn_Timer>"
    - execute as_server "sentry speed <cons:aoh_Movement_Speed>"
    - execute as_server "sentry strength <cons:aoh_str>"
    - execute as_server "sentry armor <cons:aoh_AC>"
    - execute as_server "sentry range <cons:aoh_AGRO_RADIUS>"

    on spawn:
    # Warp Avatar of Hakkar to its spawn point on spawn.
    - teleport <npc> "location:<cons:aoh_SpawnPoint>"
    # Clear previous targets from hate list
    - execute as_server "npc sel <npc.id>"
    - execute as_server "sentry target clear"
    - execute as_server "sentry target add entity:player"
    # Clear boss flags
    - flag npc BossPlayerList:!
    - flag npc AOH_PoisonAE_CD:!
    - flag npc AOH_Knockback_CD:!
    - flag npc AOH_AddSummon_CD:!
    - flag npc AOH_Sacrifice_CD:!
    - flag npc AOH_Grace_Period_CD:!

    on death:
    # XP amount
    - drop XP qty:<cons:aoh_XP_AMT> location:<npc.location>
    # Reset npc effects and clear hate targets.
    - execute as_server "npc sel <npc.id>"
    - execute as_server "npc effect --play none"
    - execute as_server "sentry target clear"
    # Announce to players the boss is dead.
    - announce "<red><npc.name> yells, <cons:death message>"
    - announce "<&5><npc.name> has been slain!"

    on enter proximity:
    - announce "<red><npc.name> yells<&cm> <&sq><cons:agro message><&sq>"
    - playsound location:<npc.location> sound:cat_hiss
    # Flag the player as 'engaged' with the boss for 30 seconds.
    # If the player attacks the NPC, this refreshes.
    # If the player gets summoned, this refreshes.
    # If the player warps to spawn or home, this duration will fade out.
    - flag player "AvatarOfHakkar_Engaged:true" duration:30

    on exit proximity:
    # summon player if they are still engaged by the boss.
    - if "<flag.p:AvatarOfHakkar_Engaged:true || false>" == true {
      - announce "<red><npc.name> yells<&cm> <&sq>You will not evade me<&cm> <player.name>!<&sq>"
      - narrate "<yellow>You have been summoned!"
      - teleport <player> "location:<npc.location>"
      - playsound location:<player.location> sound:portal_travel
      - playeffect location:<player.location> effect:portal qty:1000
      }

"AOHakkarScript":
   type: interact
   steps:
     1:
       click trigger:
         script:
         # Right Clicking on the boss tells you his % health.
         - narrate "<yellow>Avatar of Hakkar<&co> <npc.health.percentage.as_int>%"
       proximity trigger:
         entry:
           script:
           # Add player to the boss list when they enter his proximity.
           - if <npc.flag[BossPlayerList].aslist> !contains <player> && <player.name> != <npc.name> flag npc BossPlayerList:->:<player>
         move:
           script:
           # Here are the random events based on movement inside the boss' proximity
           - ^if <npc.health.percentage.as_int> <= 98 {
             - random 2
             - run "AOH_Poison_AE_Ability"
             - run "AOH_Knockback_Ability"
             }
           - ^if <npc.health.percentage.as_int> <= 50 {
             - run "AOH_Sacrifice_Ability"
             }
           # For targeting the entire group..
           #- foreach <npc.flag[BossPlayerList].aslist> {
           #  - announce "<yellow><%value%.name> Is on the BOSS HITLIST." }
       damage trigger:
         script:
           # When the boss is hit, re-flag the player as engaged with the boss.
           # We also add some flavor sound and effect. WOOOO.
           - flag player "AvatarOfHakkar_Engaged:true" duration:30
           - playsound location:<npc.location> sound:cat_hiss
           - playeffect location:<npc.location> effect:RED_DUST qty:10
           # Here are the boss special events that fire off throughout the fight.
           - ^if <npc.health.percentage.as_int> <= 98 {
             - random 2
             - run "AOH_Poison_AE_Ability"
             - run "AOH_Knockback_Ability"
             }
           - ^if <npc.health.percentage.as_int> <= 50 {
             - run "AOH_Sacrifice_Ability"
             }
           
"AOH_Knockback_Ability":
   type: task
   # This knocks the player up in the air 6 blocks every 30 seconds.
   script:
     - if <npc.flag[AOH_Knockback_CD]> == null {
       - flag npc "AOH_Knockback_CD:true" duration:30
       - announce "<red><npc.name> yells<&cm> <&sq><cons:knockback_msg><&sq>"
       - narrate "<yellow>You are knocked backwards into the air!"
       - teleport "<player.location.x.asint>,<m:<player.location.y.asint>+6>,<player.location.z.asint>,world"
       }
       
"AOH_Poison_AE_Ability":
   type: task
   # This is the boss' AoE Poison ability.
   # This hits all players on his agro list with Poison II for 5 seconds
   # every 20 seconds.
   script:
     - if <npc.flag[AOH_PoisonAE_CD]> == null {
       - flag npc "AOH_PoisonAE_CD:true" duration:20
       - announce "<red><npc.name> yells<&cm> <&sq><cons:poisonAE_msg><&sq>"
       - foreach <npc.flag[BossPlayerList].aslist> {
         - narrate targets:<%value%> "<yellow>You have become poisoned!"
         - cast poison duration:5s
         - playeffect location:<player.location> effect:potion_break qty:5 data:4
         }
       }

"AOH_Sacrifice_Ability":
   type: task
   # This ability sacrifices a player.
   # The player is rooted, blinded, and poisoned for 5 seconds.
   script:
     - if <npc.flag[AOH_Sacrifice_CD]> == null {
       - flag npc "AOH_Sacrifice_CD:true" duration:30
       - announce "<red><npc.name> yells<&cm> <&sq><cons:sacrifice_msg><&sq>"
       - narrate "<yellow>You are magically bound to the Altar of Hakkar!"
       - teleport "22,72,530,world"
       - cast poison duration:5s
       - cast blindness duration:5s
       - cast slow duration:5s power:25
       - playeffect location:<player.location> effect:ENDER_SIGNAL qty:50
       }