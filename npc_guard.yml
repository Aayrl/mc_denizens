# Generic Guard NPC
# Tyler J. Sawyer
# 4/14/14
# Assigned to all guards in-game.

guard_gondor:
  type: assignment

  default constants:
    attack message: <white>Time to die<&cm> <n@<npc.name>.navigator.target_entity.name>!
    death message: <white>I died in honor of Gondor!
    victory message: <white>You were foolish to challenge the Gondorian Guard!
    hate message: <white>Your kind is not welcome here<&cm> <n@<npc.name>.navigator.target_entity.name>!

    # Stats
    guard_str: 10
    guard_xp: 25
    guard_health: 150
    guard_respawn_delay: 60
    
    factionHit: 50
    
  interact scripts:
  - 10 GuardInteractScript

  actions:
    on assignment:
    - trigger name:damage toggle:true

    # Setup Guard Stats
    - execute as_server "npc sel <npc.id>"
    - execute as_server "npc type player"
    - execute as_server "npc speed 1"
    - execute as_server "npc lookclose"
    - execute as_server "trait sentry"
    - execute as_server "sentry health <cons:guard_health>"
    - execute as_server "sentry spawn"
    - execute as_server "sentry respawn <cons:guard_respawn_delay>"
    - execute as_server "sentry speed 1"
    - execute as_server "sentry strength <cons:guard_str>"
    - execute as_server "sentry killdrops false"
    - execute as_server "sentry healrate 1"
    - execute as_server "sentry retaliate true"
    - execute as_server "sentry targetable true"
    - execute as_server "sentry target add entity:monster"
    - execute as_server "sentry target add entity:PvP"
    - execute as_server "sentry target add entity:PvNPC"
    - execute as_server "sentry equip 267"
    - execute as_server "sentry equip 306"
    - execute as_server "sentry equip 307"
    - execute as_server "sentry equip 308"
    - execute as_server "sentry equip 309"

    on spawn:
    - trigger name:proximity toggle:true radius:15
    # Clear previous targets from hate list
    - execute as_server "npc sel <npc.id>"
    - execute as_server "sentry target clear"
    - execute as_server "sentry target add entity:monster"
    - execute as_server "sentry target add event:PvP"
    - execute as_server "sentry target add event:PvNPC"
    - flag npc AgroList:!

    on death:
    - foreach <l@<npc.location>.find.players.within[<15>].aslist> {
      - narrate targets:%value% "<npc.name> says<&cm> <&sq><cons:death message><&sq>" }
    # XP amount
    - drop XP qty:<cons:guard_xp> location:<npc.location>
    # Random loot
    - define roll <util.random.int[1].to[100]>
    - if %roll% >= 90 {
      - drop "Dented_Militia_Helm" location:<npc.location> }
    # Reset npc effects and clear hate targets.
    - execute as_server "npc sel <npc.id>"
    - execute as_server "npc effect --play none"
    - execute as_server "sentry target clear"
    # Faction Hit
    - foreach <npc.flag[AgroList].aslist> {
      - if <%value%.flag[FACTION_Gondor]> >= 1 {
        - flag player:%value% "FACTION_Gondor:<math.as_int:<%value%.flag[FACTION_Gondor]>-<cons:factionHit>>"
        - narrate targets:%value% "<&9>Your faction standing with Gondor has gotten worse! (<%value%.flag[FACTION_Gondor]>)" }
        else {
        - flag player:%value% "FACTION_Gondor:0"
        - narrate targets:%value% "<&9>Your faction standing with Gondor could not possibly get any worse! (0)" }
      }

    on attack: 
    - foreach <l@<npc.location>.find.players.within[<15>].aslist> {
      - narrate targets:%value% "<npc.name> says<&cm> <&sq><cons:attack message><&sq>" }
    
    on kill:
    - foreach <l@<npc.location>.find.players.within[<15>].aslist> {
      - narrate targets:%value% "<npc.name> says<&cm> <&sq><cons:victory message><&sq>" }
    
    on enter proximity:
    # If the player's faction is too low, guards auto-attack.
    - if <player.flag[FACTION_Gondor]> <= 5000 {
      - attack
      - if <npc.flag[AgroList].aslist> !contains <player> && <player.name> != <npc.name> flag npc AgroList:->:<player>
      - foreach <l@<npc.location>.find.players.within[<15>].aslist> {
        - narrate targets:%value% "<npc.name> says<&cm> <&sq><cons:hate message><&sq>" }
      }

"GuardInteractScript":
   type: interact
   steps:
     1:
       click trigger:
         script:
         - narrate "<yellow><npc.name><&co> <npc.health.percentage.as_int>%"
       damage trigger:
         script:
           # Add player to the agro list when they hit the guard.
           - if <npc.flag[AgroList].aslist> !contains <player> && <player.name> != <npc.name> flag npc AgroList:->:<player>
